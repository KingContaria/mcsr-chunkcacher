plugins {
    alias libs.plugins.fabric.loom
    id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://jitpack.io" }
}

loom {
    decompilers {
        vineflower {
            options.putAll(["mark-corresponding-synthetics": "1", "indent-string": "    "])
        }
    }
    mixin {
        useLegacyMixinAp = false
    }
}

dependencies {
    minecraft libs.minecraft
    mappings variantOf(libs.yarn.mappings) { classifier "v2" }
    modImplementation libs.fabric.loader
    vineflowerDecompilerClasspath libs.vineflower

    // check for the latest versions at https://jitpack.io/#kingcontaria/atum-rewrite
    modImplementation ("com.github.KingContaria:atum-rewrite:c6eb3d183b") {
        transitive = false
    }

    // we need this for Atum
    // check for the latest versions at https://jitpack.io/#kingcontaria/speedrunapi
    modImplementation "com.github.KingContaria:SpeedrunAPI:3d7827a9c1"
}

processResources {
    filesMatching "fabric.mod.json", {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(8)
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

jar {
    from "LICENSE"
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
